#
# Standard macros provided with ifeffit distribution
# customize as needed.

macro pf "A.dat" "2" ""
   "simple plot of a column file"
   read_data($1, type=raw, group=pf)
   plot(pf.1,   pf.$2,  $3)
end macro

macro rf  "A.dat" "group"
   "simple raw read of a data file"
   read_data($1, group=$2, type='raw')
end macro

macro bkg "X.dat",  "my" ""
  'very simple autobk for an xmu file'
   read_data( $1,group=$2, type=xmu)
   kweight = 1	
   rbkg    = 1.01
   spline ($2.energy, $2.xmu, kmin = 0, kmax = 0, $3)
   newplot($2.energy, $2.xmu, color = blue)
   plot   ($2.energy, $2.bkg, style = dashed, color = tomato )
end macro

macro pre "x.dat"  "dat"
  'very simple pre-edge for an xmu file'
  read_data( $1, group=$2, type=xmu)
  pre_edge($2.energy, $2.xmu)
  print ' e0 = ', e0, '  edge_step = ', edge_step
  plot ($2.energy, $2.norm)
end macro

macro testplot
  'make a standard "test-pattern" plot'
  my.x  = range(0.1,10,0.1)
  my.y1 = (exp(-my.x / 13) *  2* cos(my.x *2.3 ))
  my.y2 = (exp(-my.x/ 7.5) * (20/sqrt(my.x))  * sin(my.x/5) * 
          (cos(my.x*4)) +  cos((my.x+2.50)*2) / 4 )
  newplot(my.x,my.y1, color=red)
     plot(my.x,my.y2, color=blue)
end macro
 
macro feff_path
  'simple read of a feff path by index'
  path ($1,   feff$1.dat)
end macro

macro srfit 
  'standard r-space plot of Feffit result'
   read_data($1r.dat, type = rsp, group = dat)
   read_data($1r.fit, type = rsp, group = fit)
   newplot(r,chir_mag, group=dat, $2)
      plot(r,chir_mag, group=fit, $3)
end macro

macro skfit 
  'standard un-weighted k-space plot of Feffit result'
   read_data($1k.dat, type = chi, group = dat)
   read_data($1k.fit, type = chi, group = fit)
   newplot(k,chi, group=dat, $2)
      plot(k,chi, group=fit, $3)
end macro

macro skwfit 
  'standard k-weighted k-space plot of Feffit result'
   read_data($1k.dat, type = chi, group = dat)
   read_data($1k.fit, type = chi, group = fit)
   dat.chik = dat.chi * dat.k^($2)
   fit.chik = fit.chi * fit.k^($2)
   newplot(k,chik, group=dat, $3)
      plot(k,chik, group=fit, $4)
end macro
 
macro sqfit 
  'standard q-space plot of Feffit result'
   read_data($1q.dat, type = chi, group = qdat)
   read_data($1q.fit, type = chi, group = qfit)
   newplot(k,chi, group=qdat, $2)
      plot(k,chi, group=qfit, $3)
end macro

macro make_hardcopy ""  "/cps"
  'create image file of current plot'
  plot(file= $1, device=$2)
end macro

macro make_gif "ifeffit.gif"
 'create GIF of current plot'
  make_hardcopy  $1 "/gif"
end macro

macro make_ps "ifeffit.ps"
 'create PS of current plot'
  make_hardcopy  $1 "/cps"
end macro

macro make_bwps "ifeffit.ps"
 'create Black&White PS of current plot'
  make_hardcopy  $1 "/ps"
end macro

macro make_png "ifeffit.png" 
 'create PNG of current plot'
  make_hardcopy  $1 "/png"
end macro

